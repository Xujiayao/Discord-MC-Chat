apply plugin: "fabric-loom"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = archives_base_name
version = mod_version
group = maven_group

dependencies {
    minecraft("com.mojang:minecraft:${minecraft_version}")
    mappings("net.fabricmc:yarn:${yarn_mappings}:v2")

    modImplementation("net.fabricmc:fabric-loader:${loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_version}")

    include("net.dv8tion:JDA:5.0.0-beta.11") {
        exclude module: "opus-java"
    }
    include("net.sf.trove4j:trove4j:3.0.3")
    include("org.apache.commons:commons-collections4:4.4")
    include("org.slf4j:slf4j-api:1.7.36")
    include("com.neovisionaries:nv-websocket-client:2.14")
    include("com.fasterxml.jackson.core:jackson-core:2.14.1")
    include("com.fasterxml.jackson.core:jackson-databind:2.14.1")
    include("com.fasterxml.jackson.core:jackson-annotations:2.14.1")
    include("com.squareup.okhttp3:okhttp:4.11.0") // Use 4.11.0 instead of 4.10.0 for cleaner logs
    include("com.squareup.okio:okio:2.10.0") // Cannot use okio-jvm (#55)
    include("org.jetbrains.kotlin:kotlin-stdlib:1.6.20")

    include("com.vdurmont:emoji-java:5.1.1")
    include("org.json:json:20230227") // Vulnerabilities exist in older versions

    include("com.google.code.gson:gson:2.10.1")
}

def fabric_subprojects = parent.subprojects.findAll({
    it.name != "wrapper"
})

remapJar {
    outputs.upToDateWhen { false }

    dependsOn(fabric_subprojects.collect {
        it.tasks.remapJar
    })

    doFirst {
        delete fileTree("build/tmp/versions/META-INF/jars")
        copy {
            from {
                fabric_subprojects.collect {
                    it.tasks.remapJar.outputs.files
                }
            }
            into("build/tmp/versions/META-INF/jars")
        }
    }

    from("../LICENSE") {
        rename { "${it}_${archives_base_name}" }
    }
    from("build/tmp/versions")
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": mod_version
    }
}