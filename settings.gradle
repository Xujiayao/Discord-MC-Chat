pluginManagement {
	repositories {
		maven {
			name = "Fabric"
			url = "https://maven.fabricmc.net/"
		}
		maven {
			name = "Jitpack"
			url = "https://jitpack.io"
		}
		mavenCentral()
		gradlePluginPortal()
	}
	resolutionStrategy {
		eachPlugin {
			switch (requested.id.id) {
				case "com.replaymod.preprocess": {
					useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
					break
				}
			}
		}
	}
}

def versions = Arrays.asList(
		"1.15.2",
		"1.16.5",
		"1.17.1",
		"1.18.2",
		"1.19"  ,
		"1.19.2",
		"1.19.3",
		"1.19.4",
		"1.20.1",
		"1.20.2",
		"1.20.4",
		"1.21.1",
		"1.21.4",
		"1.21.5",
		"1.21.6",
		"1.21.7"
)
for (String version : versions) {
	include(":$version")

	def proj = project(":$version")
	proj.projectDir = file("versions/$version")
	proj.buildFileName = "../../common.gradle"
}

include(":wrapper")
