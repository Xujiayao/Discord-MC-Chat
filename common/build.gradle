import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer

apply plugin: "com.gradleup.shadow"
apply plugin: "net.neoforged.moddev"

version = mod_version

base {
    archivesName = mod_name + "-common"
}

dependencies {
    shadow("net.dv8tion:JDA:5.6.1") {
        exclude module: "opus-java" // for encoding audio into opus
        exclude module: "tink" // for encrypting and decrypting audio
        exclude module: "slf4j-api"
    }

    shadow("org.slf4j:slf4j-api:2.0.17")
    shadow("org.apache.logging.log4j:log4j-slf4j2-impl:2.25.1")
}

neoForge {
    version = "21.8.23"
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
    options.encoding = "UTF-8"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    manifest {
        attributes "Main-Class": "com.xujiayao.discord_mc_chat.common.DMCC"
    }
}

shadowJar {
    dependsOn assemble

    archiveBaseName.set("${mod_name}")
    archiveClassifier.set("")
    configurations = [project.configurations.shadow]
    mergeServiceFiles()
    minimize {
        exclude(dependency("org.apache.logging.log4j:log4j-slf4j2-impl"))
    }

    exclude "**/*-info.class"
    exclude "META-INF/maven/"

    transform(Log4j2PluginsCacheFileTransformer.class)

    relocate "net", "dmcc_dep.net"
    relocate "org", "dmcc_dep.org"
    relocate "kotlin", "dmcc_dep.kotlin"
    relocate "okhttp3", "dmcc_dep.okhttp3"
    relocate "okio", "dmcc_dep.okio"
    relocate "gnu", "dmcc_dep.gnu"
    relocate "com.neovisionaries", "dmcc_dep.com.neovisionaries"
    relocate "com.fasterxml", "dmcc_dep.com.fasterxml"
}

//========== Merge JARs ==========

def otherSubprojects = rootProject.subprojects.findAll { it.name != project.name }

tasks.register("mergeJars") {
    dependsOn otherSubprojects.collect { ":$it.name:jar" }
    dependsOn shadowJar

    def shadowJarFile = tasks.shadowJar.archiveFile.get().asFile
    def tempDir = project.layout.buildDirectory.dir("merged_temp").get().asFile

    doLast {
        tempDir.mkdirs()
        def addedFiles = [:]
        otherSubprojects.each { subproj ->
            def jarFile = subproj.tasks.named("jar").get().archiveFile.get().asFile
            copy {
                from zipTree(jarFile)
                into tempDir
                exclude "META-INF/MANIFEST.MF"
                eachFile { fcd ->
                    def relPath = fcd.relativePath.toString()
                    if (addedFiles.containsKey(relPath)) {
                        logger.warn("Warning: Duplicate file found: $relPath in ${subproj.name} and ${addedFiles[relPath]}")
                    } else {
                        addedFiles[relPath] = subproj.name
                    }
                }
            }
        }
        // 将解压后的内容加入 common 的 jar
        ant.zip(update: "true", destfile: shadowJarFile) {
            fileset(dir: tempDir)
        }
    }
}

build {
    dependsOn shadowJar
    dependsOn tasks.named("mergeJars")

    doLast {
        copy {
            from "build/libs"
            into "../build"
            exclude "*common*"
        }
    }
}
